buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:1.0.0'

		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

allprojects {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			url 'http://updates.jmonkeyengine.org/maven'
		}
		maven {
			url 'http://mthwate.com/maven'
		}
	}
}

configure(subprojects.findAll {!it.name.endsWith('-android')}) {

	apply plugin: 'java'

	sourceCompatibility = 1.7

	dependencies {
		testCompile "junit:junit:4.11"
	}

	task fatJar(type: Jar) {
		manifest = jar.manifest
		baseName = project.name + '-all'
		from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
		with jar
	}

	task bareFatJar(type: Jar) {
		manifest = jar.manifest
		baseName = project.name + "-bare-all"
		exclude 'entities/**'
		exclude 'models/**'
		exclude 'textures/**'
		exclude 'tiles/**'
		from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
		with jar
	}

	task bareJar(type: Jar) {
		manifest = jar.manifest
		baseName = project.name + "-bare"
		exclude 'entities/**'
		exclude 'models/**'
		exclude 'textures/**'
		exclude 'tiles/**'
		with jar
	}
}

def jme_version = '3.0.10'

configure(subprojects.findAll {it.name.endsWith('-desktop')}) {
	dependencies {
		runtime "com.jme3:jme3-lwjgl:$jme_version"
		runtime "com.jme3:jme3-desktop:$jme_version"
	}
}